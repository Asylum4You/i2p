plugins {
    id 'war'
}

sourceSets {
    main {
        java {
            srcDir 'java/src'
        }
    }
    test {
        java {
            srcDir 'java/test/junit'
        }
    }
}

dependencies {
    compile project(':core')
    compile project(':apps:ministreaming')
    compile 'gnu.getopt:java-getopt:1.0.13'
    providedCompile project(':apps:jetty')
}

task i2ptunnelJar(type: Jar) {
    from sourceSets.main.output 
    exclude '**/ui/*.class'
    exclude '**/EditBean.class'
    exclude '**/IndexBean.class'
    manifest {
        attributes 'Main-Class': 'net.i2p.i2ptunnel.I2PTunnel'
        attributes 'Class-Path': 'i2p.jar mstreaming.jar'
    }
    from ('resources', {
        into "net/i2p/i2ptunnel/resources"
    })
    from ('../routerconsole/jsp', {
        include 'themes/console/*/*.css'
        include 'themes/console/images/i2plogo.png'
        include 'themes/console/images/favicon.ico'
        include 'themes/console/images/itoopie_sm.png'
        into "net/i2p/i2ptunnel/resources"
    })
}

task tempBeansJar(type: Jar) {
    from sourceSets.main.output
    include '**/EditBean.class'
    include '**/ui/*.class'
    include '**/IndexBean.class'
    baseName = 'temp-beans'
}

task uiJar(type: Jar) {
    baseName = 'i2ptunnel-ui'
    from sourceSets.main.output
    include '**/ui/*.class'
    manifest {
        attributes 'Class-Path' : 'i2p.jar mstreaming.jar i2ptunnel.jar'
    }
}

artifacts {
    archives i2ptunnelJar, tempBeansJar, uiJar
}

war {
    from 'jsp'
    rootSpec.exclude('/net/i2p/i2ptunnel/*.class')
    rootSpec.exclude('/net/i2p/i2ptunnel/access')
    rootSpec.exclude('/net/i2p/i2ptunnel/irc')
    rootSpec.exclude('/net/i2p/i2ptunnel/localServer')
    rootSpec.exclude('/net/i2p/i2ptunnel/socks')
    rootSpec.exclude('/net/i2p/i2ptunnel/streamr')
    rootSpec.exclude('/net/i2p/i2ptunnel/udp')
    rootSpec.exclude('/net/i2p/i2ptunnel/udpTunnel')
    exclude 'jsp/web.xml'
    exclude '*.jsi'
    exclude '*.jsp'
    webXml = file('jsp/web.xml')
    // FIXME why is this in there? WEB-INF/lib/mstreaming.jar
}

